apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'guru.stefma.bintrayrelease'
apply plugin: 'jacoco'

version = '8.1.0'

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 50
        versionName project.version
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }

        unitTests.all {
            useJUnitPlatform()
        }
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['compileDebugSources']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.from = files(mainSrc)
    classDirectories.from = files(debugTree)
    executionData.from = fileTree(dir: project.buildDir, includes: [
            "jacoco/testDebugUnitTest.exec", // Unit tests
            "outputs/code_coverage/debugAndroidTest/connected/*coverage.ec" // Android tests
    ])
}

tasks.withType(Javadoc).all {
    enabled = false
}

ext {
    rendererRecyclerViewAdapterVersion = '3.0.0'
    tokenautocompleteVersion = '3.0.2'
    constraintLayoutVersion = '2.0.4'
    picassoVersion = '2.71828'
    inlineActivityResultVersion = '1.0.2'
    imagepickerVersion = '1.11'
    testCoreVersion = '1.3.0'
    testExtJunitVersion = '1.1.2'
    testRulesVersion = '1.3.0'
    testExpressoVersion = '3.3.0'
    kotlintestVersion = '3.4.2'
    mockkVersion = '1.10.2'
    log4jVersion = '2.14.0'
    inputMaskVersion = '6.0.0'
    androidSVGVersion = '1.4'
    expandablelayoutVersion = '2.9.2'
    threetenabpVersion = '1.3.0'
    wheelpickerVersion = '1.1.3'
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Androidx
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion",
            "com.google.android.material:material:$materialVersion",
            "androidx.recyclerview:recyclerview:$androidxRecyclerviewVersion",
            "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion",
            "androidx.multidex:multidex:$multidexVersion"

    // RendererRecyclerViewAdapter
    api "com.github.vivchar:RendererRecyclerViewAdapter:$rendererRecyclerViewAdapterVersion"

    // Token Autocomplete
    api "com.splitwise:tokenautocomplete:$tokenautocompleteVersion@aar"

    // Picasso (Image Manipulation)
    api "com.squareup.picasso:picasso:$picassoVersion"

    //Inline Activity Result (To call image picker inline)
    implementation "com.github.florent37:inline-activity-result-kotlin:$inlineActivityResultVersion"

    // ImagePicker
    api "com.thejuki:imagepicker:$imagepickerVersion"

    // Input mask
    api "com.redmadrobot:input-mask-android:$inputMaskVersion"

    // Android SVG
    implementation "com.caverock:androidsvg-aar:$androidSVGVersion"

    // Expandable Layout (To use InlineDatePickerElement)
    implementation "net.cachapa.expandablelayout:expandablelayout:$expandablelayoutVersion"

    // ThreeTen Android Backport (To use InlineDatePickerElement)
    api "com.jakewharton.threetenabp:threetenabp:$threetenabpVersion"

    // WheelPicker (To use InlineDatePickerElement)
    implementation "cn.aigestudio.wheelpicker:WheelPicker:$wheelpickerVersion"

    // Test dependencies
    testImplementation "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintestVersion"
    androidTestImplementation "androidx.test:core:$testCoreVersion"
    androidTestImplementation "androidx.test.ext:junit:$testExtJunitVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testExpressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$testExpressoVersion"
}

publish {
    version = project.version
    group = "com.thejuki"
    userOrg = 'thejuki'
    artifactId = 'k-form-master'
    desc = 'Easily build generic forms with minimal effort (A Kotlin port of FormMaster)'
    website = 'https://github.com/TheJuki/KFormMaster'
}

