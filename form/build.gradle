apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "guru.stefma.bintrayrelease"
apply plugin: 'jacoco'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 33
        versionName "6.3.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        animationsDisabled true

        unitTests {
            includeAndroidResources = true
        }
    }
}

jacoco {
    toolVersion = "$jacocoVersion"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*', '**/*$[0-9].*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code-coverage/connected/*coverage.ec'
    ])
}

tasks.withType(Javadoc).all {
    enabled = false
}

ext {
    androidxVersion = '1.0.0'
    materialVersion = '1.0.0'
    rendererRecyclerViewAdapterVersion = '2.7.0-SNAPSHOT'
    tokenautocompleteVersion = '2.0.8'
    junitVersion = '4.12'
    testExtJunitVersion = '1.1.0'
    testRulesVersion = '1.1.1'
    testExpressoVersion = '3.1.1'
    kotlintestVersion = '2.0.7'
    mockitoVersion = '2.24.0'
}

dependencies {
    implementation "androidx.appcompat:appcompat:$androidxVersion",
            "com.google.android.material:material:$materialVersion",
            "androidx.recyclerview:recyclerview:$androidxVersion"
    api "com.github.vivchar:RendererRecyclerViewAdapter:$rendererRecyclerViewAdapterVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Token Autocomplete
    api "com.splitwise:tokenautocomplete:$tokenautocompleteVersion@aar"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "io.kotlintest:kotlintest:$kotlintestVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "androidx.test.ext:junit:$testExtJunitVersion"
    androidTestImplementation "androidx.test:rules:$testRulesVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$testExpressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$testExpressoVersion"
}

publish {
    version = "6.3.0"
    group = "com.thejuki"
    userOrg = 'thejuki'
    artifactId = 'k-form-master'
    desc = 'Easily build generic forms with minimal effort (A Kotlin port of FormMaster)'
    website = 'https://github.com/TheJuki/KFormMaster'
}

